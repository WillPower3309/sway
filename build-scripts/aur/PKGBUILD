# Maintainer: Erik Reider <erik.reider@protonmail.com>
_pkgname=swayfx
pkgname="${_pkgname}"
pkgver=0.3.2
pkgrel=5
license=("MIT")
pkgdesc="SwayFX: Sway, but with eye candy!"
makedepends=(
	"git"
	"meson"
	"scdoc"
	"wayland-protocols"
)
depends=(
	"cairo"
	"gdk-pixbuf2"
	"libevdev.so"
	"libinput"
	"libjson-c.so"
	"libudev.so"
	"libwayland-server.so"
	"libwlroots.so=11"
	"libxcb"
	"libxkbcommon.so"
	"pango"
	"pcre2"
	"ttf-font"
)
optdepends=(
	"alacritty: Terminal emulator used by the default config"
	"dmenu: Application launcher"
	"grim: Screenshot utility"
	"i3status: Status line"
	"mako: Lightweight notification daemon"
	"slurp: Select a region"
	"swayidle: Idle management daemon"
	"swaylock: Screen locker"
	"wallutils: Timed wallpapers"
	"waybar: Highly customizable bar"
	"xdg-desktop-portal-gtk: Default xdg-desktop-portal for file picking"
	"xdg-desktop-portal-wlr: xdg-desktop-portal backend"
)
backup=("etc/swayfx/config")
arch=("i686" "x86_64")
url="https://github.com/WillPower3309/swayfx"
source=(
	"${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
	"50-systemd-user.conf"
	"swayfx-portals.conf"
)
sha512sums=(
	"SKIP"
	"a3f3ead4e746c003856e7e2ce714e992e66d9903c1e7984c428f4596f6467ce5cc2900ad9b0138fab00aad4b65f358d1c335aeacae107183fb24e4c8d025bf96"
	"790741df028822bf4d83170dea57e1c63f7d7938cf31969e4cd347b0fc07330322b603c9ec0091b7a3f425132bed9dee6f261074cc273555120858beaaaf5da1"
)
options=("debug")
install="swayfx.install"

build() {
	export PKG_CONFIG_PATH='/usr/lib/wlroots0.16/pkgconfig'
	arch-meson \
		-Dwerror=false \
		-Dsd-bus-provider=libsystemd \
		"${_pkgname}-${pkgver}" build
	meson compile -C build
}

package() {
	install -Dm644 50-systemd-user.conf -t "${pkgdir}/etc/swayfx/config.d/"
	install -Dm644 swayfx-portals.conf "${pkgdir}/usr/share/xdg-desktop-portal/swayfx-portals.conf"

	DESTDIR="${pkgdir}" meson install -C build

	cd "${_pkgname}-${pkgver}"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
	for util in autoname-workspaces.py inactive-windows-transparency.py grimshot; do
		install -Dm755 "contrib/${util}" -t "${pkgdir}/usr/share/${_pkgname}/scripts"
	done
}
